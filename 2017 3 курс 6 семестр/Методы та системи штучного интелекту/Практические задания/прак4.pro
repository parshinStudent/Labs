тварина(гепард):-клас(ссавець), клас("м'ясоїд"), вірно("має рудий колір"), вірно("має темні плями"),!. 
тварина(тигр):-клас(ссавець), клас("м'ясоїд"), вірно("має рудий колір"), вірно("має темні смуги"),!. 
тварина(жираф):-клас(копитне), вірно("має довгу шию"), вірно("має довгі ноги"), вірно("має темні плями"),!. 
тварина(зебра):-клас(копитне), вірно("має темні смуги"),!. 
тварина(страус):-клас(птиця), невірно("літає"), вірно("має довгу шию"), вірно("має довгі ноги"), вірно("має чорно-біле забарвлення"),!. 
тварина(пінгвін):-клас(птиця), невірно("літає"), вірно("плаває"), вірно("має чорно-біле забарвлення"),!. 
тварина(альбатрос):-клас(птиця),вірно("добре літає"),!. 
клас(ссавець):- вірно("має шерсть"),!. 
клас(ссавець):- вірно("годує дитинчат молоком"),!. 
клас(птиця):-   вірно("має пір'я"),!. 
клас(птиця):-вірно("літає"), вірно("кладе яйця"),!. 
клас("м'ясоїд"):-вірно("їсть м'ясо"),!. 
клас("м'ясоїд"):-вірно("має гострі зуби"), вірно("має пазурі"), вірно("має очі, спрямовані вперед"),!. 
клас(копитне):-клас(ссавець), вірно("має копита"),!. 
клас(копитне):-клас(ссавець), вірно("є жвачним"),!. 

вірно(X):-відповідь_так(X),!. %вже була дана позитивна відповідь, перевірка закінчена 
вірно(X):-%якщо була негативна відповідь, то перевірка буде неуспішною (not має неуспіх). 
not(відповідь_ні(X)), %інакше (якщо ще не було ніякої відповіді), спитаємо людину 
спитати(X,так). /*якщо відповідь була позитивною, то спитати(X, так) завершиться успішно, і весь предикат вірно/1 поверне істину; якщо ж відповідь негативна то вся перевірка буде неуспішною*/ %для невірно/1 все навпаки 
невірно(X):-відповідь_ні(X),!. %була дана негативна відповідь 
невірно(X):-not(відповідь_так(X)), %у випадку наявності позитивної відповіді в базі знань перевірка завершується неуспішно 
спитати(X,ні). %інакше спитаємо користувача 
спитати(X,Answer):- %X - властивість, Answer - відповідь 
write("Воно ", X, "? (y/n)"),nl, %виведемо запитання 
readln("y"),!, %якщо користувач ввів "y" (позитивна відповідь), 
запамятати(X,так),Answer=так; %то запам'ятаємо її 
запамятати(X,ні),Answer=ні. %інакше запам'ятаємо негативну відповідь 
запамятати(X,так):-assert(відповідь_так(X)). 
запамятати(X,ні):-assert(відповідь_ні(X)).   
очистити:-retract(відповідь_так(_)),fail. %видалимо всі факти відповідь_так 
очистити:-retract(відповідь_ні(_)),fail. %видалимо всі факти відповідь_ні 
очистити. %врешті-решт виклик очистити/0 закінчимо успішно 

старт:-тварина(X), %запустимо процес визначення тварини 
write("Мабуть, це ",X, "."),nl,nl, %виведемо відповідь 
очистити. %видалимо всі дані про відповіді людини 
старт:-
/*якщо в базі немає тварини з вказаними характеристиками, то виклик тварина(X) завершиться невдачею, і ми потрапимо сюди*/ 
write("Я не знаю такої тварини."),nl,nl, 
очистити. %видалимо всі дані про відповіді людини 

експерт:-старт,!, %почнемо діалог 
write("Спробуємо ще? (y/n)"),nl, readln("y"), експерт. %якщо людина хоче продовжувати, то почнемо все з початку 
/*коли в базі ще немає жодного факту відповідь_так або відповідь_ні, звернення до цих предикатів викличе помилку. 
Тому потрібно записати наступні два речення*/ 
відповідь_так(_):-fail. 
відповідь_ні(_):-fail.